#!/usr/bin/env perl6
use v6;
use Sustenance;
use Sustenance::Parser::ParseTree;

multi sub MAIN(
    Str:D $file,
    Str:D :date($d)! where .so,
    Str:D :time($t)! where .so
    --> Nil
)
{
    my Sustenance $sustenance .= new(:$file);
    my Date $date .= new($d);
    my (Str:D $h, Str:D $m, Str:D $s) = $t.split(':');
    my UInt:D $hour = Int($h);
    my UInt:D $minute = Int($m);
    my Rat:D $second = Rat($s);
    my Time $time .= new(:$hour, :$minute, :$second);
    say($sustenance.gen-macros($date, $time));
}

multi sub MAIN(
    Str:D $file,
    Str:D :date($d)! where .so
    --> Nil
)
{
    my Sustenance $sustenance .= new(:$file);
    my Date $date .= new($d);
    say($sustenance.gen-macros($date));
}

multi sub MAIN(
    Str:D $file
    --> Nil
)
{
    my Sustenance $sustenance .= new(:$file);
    say($sustenance.gen-macros);
}

sub USAGE(--> Nil)
{
    constant $HELP = q:to/EOF/.trim;
    Usage:
      sustenance [--date=<yyyy-mm-dd> --time=<hh:mm:ss>] <file>
      sustenance [--date=<yyyy-mm-dd>] <file>
    EOF
    say($HELP);
}

# vim: set filetype=perl6 foldmethod=marker foldlevel=0:
